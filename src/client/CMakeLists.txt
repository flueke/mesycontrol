find_package(PythonInterp REQUIRED)

set(SETUP_PY_IN "${CMAKE_CURRENT_SOURCE_DIR}/setup.py.in")
set(SETUP_PY    "${CMAKE_CURRENT_BINARY_DIR}/setup.py")
set(STAMP       "${CMAKE_CURRENT_BINARY_DIR}/build/timestamp")

set(PYTHON_SOURCES
   "${SETUP_PY_IN}"
   "${CMAKE_CURRENT_SOURCE_DIR}/cxfreeze_init.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol_gui.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol_script_runner.py"

   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/application_model.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/command.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/config_loader.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/config.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/config_xml.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/device_description_mhv4_800v.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/device_description_mhv4.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/device_description_mscf16.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/device_description.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/generic_device_widget.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/__init__.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/mrc_command.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/mrc_connection.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/mrc_treeview.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/protocol.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/script.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/server_process.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/tcp_client.py"
   "${CMAKE_CURRENT_SOURCE_DIR}/mesycontrol/util.py"
)

configure_file(${SETUP_PY_IN} ${SETUP_PY})

# Run the generated distutils setup.py script to build the binaries and install
# into the build directory.
if (WIN32)
  # On Windows the command line does not work correctly:
  # "C:\<some-path>" becomes "C:<some-path>"
  # Using a relative prefix works and installs under the current binary dir.
  set(DISTUTILS_PREFIX "python-install")
else (WIN32)
  set(DISTUTILS_PREFIX "${CMAKE_BINARY_DIR}/python-install")
endif (WIN32)

add_custom_command(OUTPUT ${STAMP}
    # On Windows the command line does not work correctly:
    # "C:\<some-path>" becomes "C:<some-path>"
    # Using a relative prefix works and installs under the current binary dir.
    COMMAND ${PYTHON_EXECUTABLE} ${SETUP_PY} install --prefix="${DISTUTILS_PREFIX}"
    COMMAND ${CMAKE_COMMAND} -E touch ${STAMP}
    DEPENDS ${PYTHON_SOURCES})

add_custom_target(mesycontrol_clients ALL DEPENDS ${STAMP})

# Installation rules for the files generated by distutils.
if (WIN32)
    install(DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/python-install/" DESTINATION bin)
else (WIN32)
    install(DIRECTORY ${CMAKE_BINARY_DIR}/python-install/bin/
	    DESTINATION bin USE_SOURCE_PERMISSIONS)
    install(DIRECTORY ${CMAKE_BINARY_DIR}/python-install/lib/
	    DESTINATION lib USE_SOURCE_PERMISSIONS
    PATTERN "*mesycontrol_gui"
        PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ
                GROUP_EXECUTE GROUP_READ
            WORLD_EXECUTE WORLD_READ
    PATTERN "*mesycontrol_script_runner"
        PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ
                GROUP_EXECUTE GROUP_READ
            WORLD_EXECUTE WORLD_READ)
endif (WIN32)

# vim:tw=0
